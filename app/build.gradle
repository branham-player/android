apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {

    def appCenterDevKey = BRANHAM_PLAYER_APP_CENTER_DEV_KEY
    def appCenterReleaseKey = BRANHAM_PLAYER_APP_CENTER_RELEASE_KEY

    def auth0Domain = BRANHAM_PLAYER_ANDROID_AUTH0_DOMAIN
    def auth0DevClientId = BRANHAM_PLAYER_ANDROID_AUTH0_DEV_CLIENT_ID
    def auth0ProdClientId = BRANHAM_PLAYER_ANDROID_AUTH0_PRD_CLIENT_ID
    def logglyDevKey = BRANHAM_PLAYER_ANDROID_DEV_LOGGLY_KEY

    compileSdkVersion compile_sdk_version

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId application_id

        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version

        versionCode version_code
        versionName version_name

        manifestPlaceholders = [
                auth0Domain: auth0Domain,
                auth0Scheme: application_id
        ]

        buildConfigField "String", "APP_CENTER_KEY", "\"$appCenterDevKey\""

        buildConfigField "String", "AUTH0_CLIENT_ID", "\"$auth0ProdClientId\""
        buildConfigField "String", "AUTH0_DOMAIN", "\"$auth0Domain\""
        buildConfigField "String", "AUTH0_SCHEME", "\"$application_id\""

        buildConfigField "String", "LOGGLY_ENVIRONMENT", '"development"'
        buildConfigField "String", "LOGGLY_KEY", "\"$logglyDevKey\""
    }

    buildTypes {
        debug {
            applicationIdSuffix application_id_dev_suffix

            buildConfigField "String", "AUTH0_CLIENT_ID", "\"$auth0DevClientId\""
            buildConfigField "String", "AUTH0_SCHEME", "\"$application_id$application_id_dev_suffix\""

            manifestPlaceholders += [
                    auth0Scheme: "$application_id$application_id_dev_suffix"
            ]
        }

        release {
            buildConfigField "String", "APP_CENTER_KEY", "\"$appCenterReleaseKey\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dynamicFeatures = [
            ":sermons"
    ]
}

crashlytics {
    instantAppSupport true
}

dependencies {
    implementation project(':data')

    // Android Debug Database
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    // Android Support Libraries
    implementation 'androidx.appcompat:appcompat:1.1.0-beta01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    implementation 'androidx.core:core-ktx:1.2.0-alpha02'
    implementation 'androidx.fragment:fragment-ktx:1.1.0-beta01'
    implementation 'com.google.android.material:material:1.1.0-alpha07'

    //App Center
    implementation 'com.microsoft.appcenter:appcenter-analytics:2.0.0'
    implementation 'com.microsoft.appcenter:appcenter-crashes:2.0.0'

    // Auth0
    implementation 'com.auth0.android:auth0:1.15.1'

    // Butter Knife
    implementation 'com.jakewharton:butterknife:10.1.0'
    kapt 'com.jakewharton:butterknife-compiler:10.1.0'

    // Dagger
    implementation 'com.google.dagger:dagger:2.22.1'
    kapt 'com.google.dagger:dagger-compiler:2.22.1'

    // Firebase
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.google.firebase:firebase-config:18.0.0'
    implementation 'com.google.firebase:firebase-core:17.0.0'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Logging
    implementation 'com.github.tony19:timber-loggly:1.0.1'
    implementation 'com.orhanobut:logger:2.2.0'
    implementation 'com.squareup.retrofit:retrofit:2.0.0-beta2'

    // Material Progress Bar
    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.1'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.1.0-alpha05'
    implementation 'androidx.navigation:navigation-ui-ktx:2.1.0-alpha05'

    // ReKotlin
    implementation 'org.rekotlin:rekotlin:1.0.0'

    // Room
    implementation 'androidx.room:room-runtime:2.1.0'
    implementation 'androidx.room:room-rxjava2:2.1.0'
    kapt 'androidx.room:room-compiler:2.1.0'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.8'
}

// Keep at the bottom
apply plugin: 'com.google.gms.google-services'
